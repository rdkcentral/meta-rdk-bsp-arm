#@TYPE: Machine
#@NAME: armefi64-qemu-broadband
#@DESCRIPTION: Machine configuration for generic Arm SystemReady (EFI boot) compatible (RDK-B)
#@RDK_FLAVOR: rdkb
#@NEEDED_BSPLAYERS:
include conf/machine/armefi64.conf

MACHINEOVERRIDES .= ":generic:armefi64:broadband"
MACHINE_IMAGE_NAME = "rdkb-generic-broadband-image"

# runqemu: Use the tap interface as "LAN" and let QEMU
# emulate the WAN internally.
QB_NETWORK_DEVICE = "-device virtio-net,netdev=net0,mac=@MAC@"
QB_TAP_OPT = "-netdev tap,id=net0,ifname=@TAP@,script=no,downscript=no -netdev user,id=testwan -device virtio-net,netdev=testwan"

# Override removal of kernel-image* packages in meta-rdk/conf/rdk.conf
# (we want the kernel Image in our .tar.gz for btrfs subvol install)
PACKAGE_EXCLUDE:remove = "kernel-image*"

RUST_PANIC_STRATEGY="abort"

# Prevent ASIX USB 3.0<->Ethernet driver loading at startup,
# some devices operate at CDC-NCM and need that module
# to claim the device first
KERNEL_MODULE_PROBECONF += "ax88179_178a"
module_conf_ax88179_178a = "blacklist ax88179_178a"

KMACHINE="${TUNE_PKGARCH}"
STAGING_KERNEL_DIR = "${TMPDIR}/work-shared/${KMACHINE}/kernel-source"
STAGING_KERNEL_BUILDDIR = "${TMPDIR}/work-shared/${KMACHINE}/kernel-build-artifacts"
